apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'io.fabric'

android {
    signingConfigs {
        release_config {
            keyAlias 'key0'
            keyPassword 'quabynah'
            storeFile file('F:/GitHub/ug-short-course/android-applications/chat-application/keystore/chatapp.jks')
            storePassword 'quabynah'
        }
    }
    compileSdkVersion 28
    defaultConfig {
        applicationId "io.codelabs.chatapplication"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode gitCommitCount
        versionName gitSha
        archivesBaseName = "chat-app"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField 'String', 'GIT_SHA', "\"${gitSha}\""
        buildConfigField 'String', 'GIT_COMMIT', "\"${gitCommitCount}\""

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation":
                                     "$projectDir/schemas".toString()]
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release_config
        }
    }

    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }

    androidExtensions {
        experimental = true
    }
}

dependencies {
    def koin = "2.0.0-beta-1"
    def room = "2.1.0-alpha04"
    def lifecycle = "2.0.0"
    def glide = "4.8.0"
    def gson = "2.8.4"
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    // Android Libraries
    implementation 'androidx.appcompat:appcompat:1.1.0-alpha02'
    implementation 'androidx.core:core-ktx:1.1.0-alpha04'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    // Koin
    implementation "org.koin:koin-android:$koin"
    implementation "org.koin:koin-androidx-scope:$koin"
    implementation "org.koin:koin-androidx-viewmodel:$koin"
    // Room
    implementation "androidx.room:room-runtime:$room"
    kapt "androidx.room:room-compiler:$room"
    implementation "androidx.room:room-coroutines:$room"
    // Lifecycle
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle"
    kapt "androidx.lifecycle:lifecycle-compiler:$lifecycle"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle"
    // Glide
    implementation "com.github.bumptech.glide:glide:$glide"
    kapt "com.github.bumptech.glide:compiler:$glide"
    // Gson
    implementation "com.google.code.gson:gson:$gson"
    // Firebase
    implementation 'com.google.firebase:firebase-core:16.0.7'
    implementation 'com.google.firebase:firebase-firestore:18.0.1'
    implementation 'com.google.firebase:firebase-storage:16.0.5'
    implementation 'com.google.firebase:firebase-auth:16.1.0'
    implementation 'com.google.firebase:firebase-messaging:17.3.4'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.9'
    // Play Services
    implementation 'com.google.android.gms:play-services-auth:16.0.1'
    // PhotoView
    implementation 'com.github.chrisbanes:PhotoView:2.3.0'
    // Material Dialogs
    implementation 'com.afollestad.material-dialogs:core:2.0.0'
    implementation 'com.afollestad.material-dialogs:input:2.0.0'
}
apply plugin: 'com.google.gms.google-services'